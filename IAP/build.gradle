plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
    id 'maven-publish'
}

group = 'com.github.davinci9196'

android {
    namespace 'org.mg.iap'
    compileSdk androidCompileSdk

    defaultConfig {
        minSdk androidMinSdk
        //noinspection ExpiredTargetSdkVersion
        targetSdk androidTargetSdk

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.5'
    }
    buildFeatures {
        aidl = true
        buildConfig = true
        compose true
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    packagingOptions {
        exclude "**/*.proto"
    }
    publishing {
        singleVariant("release") {
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.24.0'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.builtins {
                java {
                    option "lite"
                }
                kotlin {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    //webview
    implementation 'androidx.webkit:webkit:1.7.0'

    //protobuf
    implementation 'com.google.protobuf:protobuf-javalite:3.24.0'
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.24.0'

    implementation("com.squareup.okhttp3:okhttp:4.11.0")

    //compose
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.animation:animation-graphics'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation("io.coil-kt:coil-compose:2.4.0")
    implementation("io.coil-kt:coil-svg:2.2.2")
    implementation 'com.google.android.material:material:1.9.0'

    //droidguard
    implementation 'org.microg.gms:play-services-droidguard:0.2.27.223616'

    //androidx
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.2'
    implementation "androidx.preference:preference-ktx:1.2.1"

    //json serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.0"
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = group
                artifactId = 'OpenIAP'
                version = version
            }
        }
    }
}
